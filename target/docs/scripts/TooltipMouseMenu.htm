<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
"http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>툴팁 마우스 메뉴 (XP/2k/NT 요구) -- by Rajat</title>
<meta http-equiv="content-type" content="text/html; charset=UTF-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>툴팁 마우스 메뉴 (XP/2k/NT 요구) -- by Rajat</h1>

<p>이 스크립트는 가운데 버튼을 잠깐 누르는데 반응하여 팝업 메뉴를 보여줍니다. 메뉴 항목은 왼-클릭해 선택합니다. 바깥을 왼클릭하면 메뉴가 취소됩니다. 최근에 어느 유형의 창이 활성화 되어 있는가에 따라 메뉴의 내용이 바뀌도록 개선되었습니다 (Notepad 그리고 Word가 예제에서 사용됩니다).
</p>
<p><a href="TooltipMouseMenu.ahk">이 스크립트 내려받기</a> &nbsp;| &nbsp;<a href="index.htm">다른 샘플 스크립트</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">홈으로</a></p>

<pre class="NoIndent"><em>; 메뉴에 대하여 여기에서 제목을 설정할 수 있습니다:</em>
MenuTitle = -=-=-=-=-=-=-=-

<em>; 다음은 얼마나 오래 버튼을 눌러야 메뉴를 보여줄지 결정합니다:</em>
UMDelay = 20

SetFormat, float, 0.0
SetBatchLines, 10ms 
SetTitleMatchMode, 2
#SingleInstance


<em>;___________________________________________
;_____메뉴 정의______________________</em>

<em>; 여기에서 메뉴 항목을 만들고 수정합니다.
; keys/values/section 이름에 스페이스를 사용하면 안 됩니다.</em>

<em>; 순서는 신경쓰지 않아도 됩니다. 메뉴는 정렬됩니다.</em>

MenuItems = Notepad/Calculator/Section 3/Section 4/Section 5


<em>;___________________________________________
;______동적인 메뉴 항목 정의_______________</em>

<em>; 구문:
;     Dyn# = 메뉴 항목|창 제목</em>

Dyn1 = MS Word|- Microsoft Word
Dyn2 = Notepad II|- Notepad

<em>;___________________________________________</em>

Exit


<em>;___________________________________________
;_____메뉴 섹션_________________________</em>

<em>; 여기에 메뉴 섹션을 생성하고 편집합니다.</em>

Notepad:
Run, Notepad.exe
Return

Calculator:
Run, Calc
Return

Section3:
MsgBox, You selected 3
Return

Section4:
MsgBox, You selected 4
Return

Section5:
MsgBox, You selected 5
Return

MSWord:
msgbox, this is a dynamic entry (word)
Return

NotepadII:
msgbox, this is a dynamic entry (notepad)
Return


<em>;___________________________________________
;_____핫키 섹션________________________</em>

~MButton::
HowLong = 0
Loop
{
    HowLong ++
    Sleep, 10
    GetKeyState, MButton, MButton, P
    IfEqual, MButton, U, Break
}
IfLess, HowLong, %UMDelay%, Return


<em>; 동적 메뉴를 준비합니다</em>
DynMenu =
Loop
{
    IfEqual, Dyn%a_index%,, Break

    StringGetPos, ppos, dyn%a_index%, |
    StringLeft, item, dyn%a_index%, %ppos%
    ppos += 2
    StringMid, win, dyn%a_index%, %ppos%, 1000

    IfWinActive, %win%,
        DynMenu = %DynMenu%/%item%
}


<em>; 정렬된 메인 메뉴와 동적 메뉴를 결합합니다</em>
Sort, MenuItems, D/
TempMenu = %MenuItems%%DynMenu%


<em>; 이전 엔트리들을 지웁니다</em>
Loop
{
    IfEqual, MenuItem%a_index%,, Break
    MenuItem%a_index% =
}

<em>; 새 엔트리들을 생성합니다</em>
Loop, Parse, TempMenu, /
{
    MenuItem%a_index% = %a_loopfield%
}

<em>; 메뉴를 생성합니다</em>
Menu = %MenuTitle%
Loop
{
    IfEqual, MenuItem%a_index%,, Break
    numItems ++
    StringTrimLeft, MenuText, MenuItem%a_index%, 0
    Menu = %Menu%`n%MenuText%
}

MouseGetPos, mX, mY
HotKey, ~LButton, MenuClick
HotKey, ~LButton, On
ToolTip, %Menu%, %mX%, %mY%
WinActivate, %MenuTitle%
Return


MenuClick:
HotKey, ~LButton, Off
IfWinNotActive, %MenuTitle%
{
    ToolTip
    Return
}

MouseGetPos, mX, mY
ToolTip
mY -= 3        <em>; 첫 줄이 시작하면 뒤에 스페이스</em>
mY /= 13    <em>; 각 줄마다 스페이스</em>
IfLess, mY, 1, Return
IfGreater, mY, %numItems%, Return
StringTrimLeft, TargetSection, MenuItem%mY%, 0
StringReplace, TargetSection, TargetSection, %a_space%,, A
Gosub, %TargetSection%
Return
</pre>
</body>
</html>
